import org.gradle.internal.os.OperatingSystem;

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    //options.compilerArgs << "-Xlint:deprecation"
}

def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystorePropertiesFilename = 'keystore.properties'
if (localProperties.getProperty('keystorePropertiesFilename') != null) {
    keystorePropertiesFilename = localProperties.getProperty("keystorePropertiesFilename")
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file(keystorePropertiesFilename)
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
}

def njobs = getProperty("j")

def localize = getProperty("localize").toBoolean()
def abi32 = getProperty("abi32").toBoolean()
def abi64 = getProperty("abi64").toBoolean()
def deps = getProperty("deps")
def override_version = getProperty("override_version")
def version_header_path = getProperty("version_header_path")

if (!abi32 && !abi64) {
    throw new GradleException("Both `abi32` and `abi64` properties are set to false")
}
if (!file(deps).exists()) {
    throw new GradleException("Dependencies file does not exist:" + deps)
}

if (!override_version.isEmpty()) {
    if (version_header_path.isEmpty()) {
        throw new GradleException("`version_header_path` cannot be empty when `override_version` is not empty")
    } else {
        println("Overriding version number to $override_version using path $version_header_path")
    }
}

task unzipDeps(type: Copy) {
    println("Using dependencies file: $deps")
    def zipFile = new File("$deps")
    def outputDir = new File('.')
    from zipTree(zipFile)
    into outputDir
}

task makeLocalization(type: Exec) {
    if (localize) {
        println("Building with localization'")
        switch (OperatingSystem.current()) {
            case OperatingSystem.LINUX:
                workingDir './../..'
                commandLine 'make', 'localization', 'LANGUAGES=all'
                break
            case OperatingSystem.WINDOWS:
                workingDir './../..'
                commandLine 'sh.exe', 'android/app/makelocalizations.sh'
                break
            default:
                workingDir './../..'
                commandLine 'echo', 'Building without localization'
                break
        }
    } else {
        workingDir './../..'
        commandLine 'echo', 'Building without localization'
    }
}

makeLocalization
unzipDeps.dependsOn makeLocalization
preBuild.dependsOn unzipDeps

android {
    compileSdkVersion 28

    if (override_version.isEmpty()) {
        println("Generating version number to $version_header_path")
        switch (OperatingSystem.current()) {
            case OperatingSystem.LINUX:
                exec {
                    workingDir './../..'
                    commandLine 'make', 'version'
                }
                break
            case OperatingSystem.WINDOWS:
                exec {
                    workingDir './../..'
                    commandLine 'sh.exe', '-c', 'make version'
                }
                break
            default:
                exec {
                    workingDir './../..'
                    commandLine 'make', 'version'
                }
                break
        }
    } else {
        println("Overriding version number to $override_version")
        exec {
            workingDir './../..'
            commandLine 'echo', '#define VERSION $override_version', '>', '$version_header_path'
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode Integer.valueOf(System.env.UPSTREAM_BUILD_NUMBER ?: 1)
        versionName new File("$version_header_path").text.split('\"')[1]
        if (buildAsApplication) {
            applicationId "com.cleverraven.cataclysmdda"
            setProperty("archivesBaseName", "cataclysmdda-" + versionName)
        }

        splits {
            // Configures multiple APKs based on ABI.
            abi {
                // Enables building multiple APKs per ABI.
                enable true
                // Resets the list of ABIs that Gradle should create APKs for to none.
                reset()
                // Specifies a list of ABIs that Gradle should create APKs for.
                if (abi32) {
                    include "armeabi-v7a"
                }
                if (abi64) {
                    include "arm64-v8a"
                }
                // Specifies that we do not want to also generate a universal APK that includes all ABIs.
                universalApk false
            }
        }

        externalNativeBuild {
            ndkBuild {
                arguments "APP_PLATFORM=android-21", "-j$njobs"
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        if (keystoreProperties.getProperty('storeFile') != null) {
            if (file(keystoreProperties.getProperty('storeFile')).exists()) {
                release {
                    storeFile file(keystoreProperties.getProperty("storeFile"))
                    storePassword keystoreProperties.getProperty("storePassword")
                    keyAlias keystoreProperties.getProperty("keyAlias")
                    keyPassword keystoreProperties.getProperty("keyPassword")
                }
            } else {
                throw new GradleException("Keystore file " + keystoreProperties.getProperty("storeFile") + " was not found.\n")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (signingConfigs.hasProperty("release")) {
                signingConfig signingConfigs.release
            }
            externalNativeBuild {
                ndkBuild {
                    cFlags "-DNDEBUG", "-DRELEASE", "-Os"
                }
            }
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            externalNativeBuild {
                ndkBuild {

                }
            }
        }
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            ndkBuild {
                path 'jni/Android.mk'
            }
        }

    }
    lintOptions {
        abortOnError false
    }

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "com.cleverraven.cataclysmdda.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
}
